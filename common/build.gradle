import com.chrynan.video.buildSrc.AppConstants
import com.chrynan.video.buildSrc.plugins.schema.CreateSchemaJsonPlugin
import com.chrynan.video.buildSrc.Deps

apply plugin: "kotlin-multiplatform"
apply plugin: "com.android.library"
apply plugin: "digital.wup.android-maven-publish"
apply plugin: "kotlinx-serialization"
apply plugin: "com.apollographql.apollo"
apply plugin: CreateSchemaJsonPlugin

group AppConstants.appGroup
version AppConstants.appVersion

kotlin {
    targets {
        jvm()
        fromPreset(presets.android, "android")
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation Deps.KOTLIN_COMMON

                implementation Deps.KOTLIN_COROUTINES_COMMON

                // HTTP
                api Deps.KTOR_CORE_COMMON
                api Deps.KTOR_SERIALIZATION
                api Deps.KTOR_LOGGING

                // GraphQL Client - Apollo
                api Deps.APOLLO_API_COMMON
                implementation Deps.APOLLO_RUNTIME_COMMON
                implementation Deps.APOLLO_CACHE_SQLITE_COMMON

                // Serialization
                api Deps.KOTLIN_SERIALIZATION_COMMON

                // Logging
                api Deps.CHRYNAN_LOGGING_CORE
            }
        }
        jvmMain {
            dependencies {
                implementation Deps.KOTLIN_JVM

                implementation Deps.JAVAX_INJECT

                implementation Deps.KOTLIN_COROUTINES_JVM

                // HTTP
                api Deps.KTOR_OKHTTP
                api Deps.KTOR_SERIALIZATION_JVM
                api Deps.KTOR_LOGGING_JVM

                // GraphQL Client - Apollo
                api Deps.APOLLO_API_JVM

                // Serialization
                api Deps.KOTLIN_SERIALIZATION_JVM

                // Logging
                api Deps.CHRYNAN_LOGGING_CORE_JVM
            }
        }
        androidMain {
            dependencies {
                implementation Deps.KOTLIN_JVM

                implementation Deps.JAVAX_INJECT

                implementation Deps.KOTLIN_COROUTINES_COMMON

                // HTTP
                api Deps.KTOR_OKHTTP
                api Deps.KTOR_SERIALIZATION_JVM
                api Deps.KTOR_LOGGING_JVM

                // GraphQL Client - Apollo
                api Deps.APOLLO_API_JVM

                // Serialization
                api Deps.KOTLIN_SERIALIZATION_JVM

                // Logging
                api Deps.CHRYNAN_LOGGING_CORE_JVM
            }
        }
    }
}

android {
    compileSdkVersion AppConstants.androidCompileSdkVersion
    defaultConfig {
        minSdkVersion AppConstants.androidMinSdkVersion
        targetSdkVersion AppConstants.androidTargetSdkVerison
        versionCode AppConstants.appVersionCode
        versionName AppConstants.appVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            matchingFallbacks = ["debug"]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    sourceSets {
        main {
            manifest.srcFile "src/androidMain/AndroidManifest.xml"
            java.srcDirs = ["src/androidMain/kotlin"]
            res.srcDirs = ["src/androidMain/res"]
        }
        test {
            java.srcDirs = ["src/androidTest/kotlin"]
            res.srcDirs = ["src/androidTest/res"]
        }
    }
}

dependencies {

}

graphQLSchemaJson {
    graphQLDirectory = "${project.rootDir.path}/api/graphql/"
    schemaOutputDirectory = "${project.projectDir.path}/src/commonMain/graphql/"
}

apollo {
    generateKotlinModels = true
    customTypeMapping = [
            "Moment"   : "kotlin.String",
            "UriString": "kotlin.String",
            "Cursor"   : "kotlin.String"
    ]
}

project.tasks.whenTaskAdded { task ->
    if (task.name == "generateApolloSources" || task.name == "generateCommonMainApolloSources") {
        task.dependsOn(createSchemaJson)
    }
}

configurations {
    compileClasspath
}

publishing {
    publications {
        kotlinMultiplatform {
            artifactId = "video-common"
        }
        jvm {
            artifactId = "video-common-jvm"
        }
        MavenPub(MavenPublication) {
            from(components.android)
            artifactId "video-common-android"
        }
        metadata {
            artifactId = "video-common-metadata"
        }
    }
}
