import com.chrynan.video.buildSrc.AppConstants

apply plugin: "kotlin-multiplatform"
apply plugin: "com.android.library"
apply plugin: "digital.wup.android-maven-publish"

group AppConstants.appGroup
version AppConstants.appVersion

kotlin {
    targets {
        jvm()
        fromPreset(presets.android, "android")
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"

                implementation project(":common")

                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.3.7"

                implementation "com.chrynan.aaaah:aaaah-core:0.7.0"
            }
        }
        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib"

                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"

                implementation "com.chrynan.aaaah:aaaah-core-jvm:0.7.0"
            }
        }
        androidMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib"

                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"

                implementation "com.chrynan.aaaah:aaaah-core-jvm:0.7.0"
            }
        }
    }
}

android {
    compileSdkVersion AppConstants.androidCompileSdkVersion
    defaultConfig {
        minSdkVersion AppConstants.androidMinSdkVersion
        targetSdkVersion AppConstants.androidTargetSdkVerison
        versionCode AppConstants.appVersionCode
        versionName AppConstants.appVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            matchingFallbacks = ["debug"]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    sourceSets {
        main {
            manifest.srcFile "src/androidMain/AndroidManifest.xml"
            java.srcDirs = ["src/androidMain/kotlin"]
            res.srcDirs = ["src/androidMain/res"]
        }
        test {
            java.srcDirs = ["src/androidTest/kotlin"]
            res.srcDirs = ["src/androidTest/res"]
        }
    }
}

configurations {
    compileClasspath
}

publishing {
    publications {
        kotlinMultiplatform {
            artifactId = "video-presentation"
        }
        jvm {
            artifactId = "video-presentation-jvm"
        }
        MavenPub(MavenPublication) {
            from(components.android)
            artifactId "video-presentation-android"
        }
        metadata {
            artifactId = "video-presentation-metadata"
        }
    }
}
