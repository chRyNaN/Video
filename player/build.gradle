import com.chrynan.video.buildSrc.AppConstants

apply plugin: "kotlin-multiplatform"
apply plugin: "com.android.library"
apply plugin: "digital.wup.android-maven-publish"

group AppConstants.appGroup
version AppConstants.appVersion

kotlin {
    targets {
        fromPreset(presets.android, "android")
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"
            }
        }
        androidMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib"
            }
        }
    }
}

android {
    compileSdkVersion AppConstants.androidCompileSdkVersion
    defaultConfig {
        minSdkVersion AppConstants.androidMinSdkVersion
        targetSdkVersion AppConstants.androidTargetSdkVerison
        versionCode AppConstants.appVersionCode
        versionName AppConstants.appVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            matchingFallbacks = ["debug"]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    sourceSets {
        main {
            manifest.srcFile "src/androidMain/AndroidManifest.xml"
            java.srcDirs = ["src/androidMain/kotlin"]
            res.srcDirs = ["src/androidMain/res"]
        }
        test {
            java.srcDirs = ["src/androidTest/kotlin"]
            res.srcDirs = ["src/androidTest/res"]
        }
    }
}

dependencies {
    // Video Player - ExoPlayer
    implementation "com.google.android.exoplayer:exoplayer:2.11.5"

    // Image Loading - Coil
    implementation "io.coil-kt:coil:0.11.0"
}

configurations {
    compileClasspath
}

publishing {
    publications {
        kotlinMultiplatform {
            artifactId = "video-player"
        }
        metadata {
            artifactId = "video-player-metadata"
        }
        MavenPub(MavenPublication) {
            from(components.android)
            artifactId "video-player-android"
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xopt-in=kotlin.RequiresOptIn"]
    }
}
